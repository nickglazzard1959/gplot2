INSTALL
*NOSEQ
.PROC,INSTALL*I,PROG=(*F),LIB=(*F).
.HELP.
INSTALL A PROGRAM INTO A USER LIBRARY FOR BINARIES.
THE NAMED LIBRARY IS MAINTAINED AS A DIRECT ACCESS PERMANENT FILE.
THE MODIFIED LIBRARY IS ADDED TO THE USER-S GLOBAL LIBRARY SET.
ALL PROGRAMS IN THE LIBRARY WILL BE AVAILABLE AS COMMANDS TO THE USER.
.HELP,PROG.
NAME OF ABS BINARY FILE TO INSTALL.
.HELP,LIB.
NAME OF THE LIBRARY FILE.
.ENDHELP.
.*
.IF,.NOT.FILE(PROG,AS),NOPROG.
REVERT,ABORT.PROG IS NOT A LOCAL FILE.
.ENDIF,NOPROG.
.*
LIBRARY,LIB/D.
RETURN,LIB.
REWIND,PROG.
ATTACH,LIB/M=W.
.*
SKIP,FEXISTS.
EXIT.
NOTE./LIBRARY LIB DOES NOT EXIST AS PF. CREATING IT.
DEFINE,LIB.
ATTACH,LIB/M=W.
NOTE./INSTALLING PROG INTO EMPTY LIBRARY LIB.
LIBEDIT,P=0,N=LIB,L=0,I=0,U=LIB,B=PROG.
RETURN,LIB,PROG.
ATTACH,LIB.
REWIND,LIB.
LIBRARY,LIB/A.
REVERT,NOLIST.
ENDIF,FEXISTS.
.*
NOTE./INSTALLING PROG INTO EXISTING LIBRARY LIB.
LIBEDIT,P=LIB,N=LIB,L=0,I=0,U=LIB,B=PROG.
RETURN,LIB,PROG.
ATTACH,LIB.
REWIND,LIB.
LIBRARY,LIB/A.
REVERT,NOLIST.
.*
EXIT.
REVERT,ABORT.INSTALL FAILED.
*WEOR
