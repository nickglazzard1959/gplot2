BUILD
*NOSEQ
.PROC,BUILD*I,ARG=(*N=NONE,*F),OPT=(*N=0,*S1..1/D).
.IF,$ARG$.EQ.$NONE$,DOLIB.
MODIFY,P=PLPROCS,C=PROCLIB,L=0,S=0,I=PRCDIR.
.IF,.NOT.FILE(USERLIB,LO),NOLIB.
NOTE./USERLIB DOES NOT EXIST. CREATING IT.
LIBEDIT,P=0,N=USERLIB,L=0,I=0,B=INFO.
.ENDIF,NOLIB.
ULIB,OP=A,REC=PROCLIB,LIB=USERLIB.
REPLACE,USERLIB.
.*RETURN,PROCLIB.
.IF,FILE(PRCDIR,LO).RETURN,PRCDIR.
.IF,FILE(INFO,LO).RETURN,INFO.
NOTE./USERLIB UPDATED FROM PLPROCS PROCEDURES.
NOTE./USE LIBRARY,USERLIB TO MAKE IT ACTIVE.
MODIFY,P=PLPROCS,C=PRLOGIN,L=0,S=0,Z./*EDIT LOGIN
REPLACE,PRLOGIN/NA.
UPROC,PRLOGIN.
NOTE./LOGIN PROCEDURE PRLOGIN SET WITH UPROC.
.ENDIF,DOLIB.
.*
.IF,$ARG$.EQ.$DOC$,DODOC.
NOTE./BUILDING PLPROCS DOCUMENTATION.
RETURN,DOCSRC.
MODIFY,P=PLPROCS,C=0,L=0,S=DOCSRC,N=0,I=PRCDIR.
PACK,DOCSRC.
PURGE,MDPROCS/NA.
DOCPROC,I=DOCSRC,O=MDPROCS.
SAVE,MDPROCS.
NOTE./MARKDOWN DOC FOR PLPROCS NOW IN FILE MDPROCS.
RETURN,COMPILE.
MODCOMP,DOC,PLPROCS.
REWIND,COMPILE.
RETURN,CONCAT.
COPYBF,COMPILE,CONCAT.
REWIND,MDPROCS.
COPYBF,MDPROCS,CONCAT.
REWIND,CONCAT.
PACK,CONCAT.
REWIND,CONCAT.
RETURN,HTPROCS.
PMDHTML,I=CONCAT,O=HTPROCS,C,L,A,F,D.
WPUT,HTPROCS.
WPUT,INDEXMD.
RETURN,DOCSRC,PRCDIR,HTPROCS,CONCAT,COMPILE,INDEXMD.
.ENDIF,DODOC.
.*
REVERT,NOLIST.
EXIT.
REVERT,ABORT.BUILD FAILED.
.DATA,PRCDIR.
*EDIT TERMSEL
*EDIT PRINT
*EDIT FTNSYS
*EDIT SENDFL
*EDIT PL2SRC
*EDIT SRC2PL
*EDIT MODPUT
*EDIT MODGET
*EDIT MODCOMP
*EDIT MODLIST
*EDIT MODEDIT
*EDIT MODEXEC
*EDIT LIBPUT
*EDIT LIBGET
*EDIT TLBPUT
*EDIT TLBGET
*EDIT MODDEL
*EDIT LIBLIST
*EDIT LIBDEL
*EDIT TLBLIST
*EDIT TLBDEL
*EDIT TLBEDIT
*EDIT RSHOW
*EDIT LF
*EDIT RCOPY
*EDIT LINKPGM
*EDIT REDIR
*EDIT MODVIEW
*EDIT WPUT
*EDIT INSTALL
*EDIT PSPRINT
*EDIT MODPUTW
*EDIT CRAYRUN
*EDIT CRAYPUT
*EDIT CRAYPEX
*EDIT CRAYINS
*EDIT CSUB612
*EDIT QGET612
*EDIT JOBS
.EOF
.DATA,INFO.
INFO
PRIMARY USER LIBRARY.
.EOF
*WEOR
