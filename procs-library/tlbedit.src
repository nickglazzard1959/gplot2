TLBEDIT
*NOSEQ
.PROC,TLBEDIT*I,MOD=(*F),LIB=(*F),SAVEIT=(*N=SAVE,*A)
,ALTNAME=(*N=NO,*A).
.HELP.
GET A MODULE FROM A TEXT LIBRARY, EDIT AND RETURN IT.
.HELP,MOD.
NAME OF THE MODULE.
.HELP,LIB.
NAME OF THE TEXT LIBRARY.
.HELP,SAVEIT.
IF OMITTED OR SAVE, SAVE MODIFIED LIBRARY AS PF.
.HELP,ALTNAME.
PUT THE MODULE INTO FILE ALTNAME INSTEAD OF MODULE NAME.
.ENDHELP.
NOTE./GETTING MODULE MOD FROM LIB.
TLBGET,MOD,LIB.
.IF,$ALTNAME$.NE.$NO$,ALT
NOTE./USING ALTERNATE FILE NAME ALTNAME.
RENAME,ALTNAME=MOD.
.SET,#MOD=$ALTNAME$.
.ENDIF,ALT.
FSE,MOD.
NOTE./PUTTING MODULE MOD INTO LIB.
TLBPUT,MOD,LIB.
RETURN,MOD.
.IF,$SAVEIT$.EQ.$SAVE$,DOSAVE.
NOTE./REPLACING LIB AS PF.
REPLACE,LIB/NA.
.ELSE,DOSAVE.
NOTE./LIB NOT SAVED AS PF.
.ENDIF,DOSAVE.
REVERT,NOLIST.
EXIT.
REVERT,ABORT.TLBEDIT.
*WEOR
