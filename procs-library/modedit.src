MODEDIT
*NOSEQ
.PROC,MODEDIT*I,MOD=(*F),LIB=(*F),SAVEIT=(*N=SAVE,*A)
,ALTNAME=(*N=NO,*A).
.HELP.
GET A MODULE FROM A SOURCE LIBRARY, EDIT AND RETURN IT.
.HELP,MOD.
NAME OF THE MODULE.
.HELP,LIB.
NAME OF THE SOURCE LIBRARY.
.HELP,SAVEIT.
IF OMITTED OR SAVE, SAVE MODIFIED LIBRARY AS PF.
.HELP,ALTNAME.
PUT THE MODULE INTO FILE ALTNAME INSTEAD OF MODULE NAME.
.ENDHELP.
.IF,.NOT.FILE(LIB,LO).REVERT,ABORT.LIBRARY LIB IS NOT LOCAL.
NOTE./GETTING MODULE MOD FROM LIB.
MODGET,MOD,LIB.
.IF,$ALTNAME$.NE.$NO$,ALT
NOTE./USING ALTERNATE FILE NAME ALTNAME.
RENAME,ALTNAME=MOD.
.SET,#MOD=$ALTNAME$.
.ENDIF,ALT.
FSE,MOD.
NOTE./PUTTING MODULE MOD INTO LIB.
MODPUT,MOD,LIB.
RETURN,MOD.
.IF,$SAVEIT$.EQ.$SAVE$,DOSAVE.
NOTE./REPLACING LIB AS PF.
REPLACE,LIB/NA.
.ELSE,DOSAVE.
NOTE./LIB NOT SAVED AS PF.
.ENDIF,DOSAVE.
REVERT,NOLIST.
EXIT.
REVERT,ABORT.MODEDIT FAILED.
*WEOR
