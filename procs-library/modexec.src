MODEXEC
*NOSEQ
.PROC,MODEXEC*I,MOD=(*F),LIB=(*F),ARG=(*N=NONE,*F),OPT=(*N=0,*S1..1/D),
PARM=(*N=NONE,*A).
.HELP.
EXTRACT A MODULE FROM A SOURCE LIBRARY AND CALL IT AS A PROCEDURE.
.HELP,MOD.
NAME OF THE MODULE.
.HELP,LIB.
NAME OF THE SOURCE LIBRARY.
.HELP,ARG.
OPTIONAL ARGUMENT TO PASS TO PROCEDURE.
.HELP,OPT.
ANOTHER OPTIONAL ARGUMENT, SINGLE DECIMAL DIGIT, FOR OPTIMIZATION.
.HELP,PARM.
OPTIONAL ARGUMENT. ANY STRING UP TO 40 CHARS. DEFAULTS TO NONE.
.ENDHELP.                 .
.IF,.NOT.FILE(LIB,LO),NOLIB.
REVERT,ABORT.LIBRARY LIB IS NOT LOCAL.
.ENDIF,NOLIB.
.IF,FILE(MOD,LO).RETURN,MOD.
MODIFY,P=LIB,N=0,L=0,S=0,C=MOD,Z./*EDIT MOD
NOTE./EXECUTING MOD. #ARG=ARG, #OPT=OPT, #PARM=$PARM$.
.IF,STR($PARM$).EQ.STR($NONE$),NOPARM.
BEGIN,MOD,MOD,#ARG=ARG,#OPT=OPT.
.ELSE,NOPARM.
BEGIN,MOD,MOD,#ARG=ARG,#OPT=OPT,#PARM=$PARM$.
.ENDIF,NOPARM.
RETURN,MOD.
NOTE./PROCEDURE COMPLETED.
REVERT,NOLIST.
EXIT.
REVERT,ABORT.MODEXEC FAILED.
*WEOR
