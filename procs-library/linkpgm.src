LINKPGM
*NOSEQ
.PROC,LINKPGM*I,OBJ=(*F),PGM=(*F),LIB1=(*N=NO,*F),LIB2=(*N=NO,*F).
.HELP.
LINK AN OBJECT FILE WITH LIBRARIES TO AN ABSOLUTE BINARY PGM.
.HELP,OBJ.
RELOCATABLE BINARY OBJECT INPUT FILE NAME.
.HELP,PGM.
ABSOLUTE BINARY PROGRAM OUTPUT NAME.
.HELP,LIB1.
OPTIONAL ADDITIONAL INPUT LIBRARY.
.HELP,LIB2.
OPTIONAL ADDITIONAL INPUT LIBRARY.
.ENDHELP.
.IF,.NOT.FILE(LBUTILS,PM),NOLIBUT.
NOTE./LBUTILS NOT ATTACHED. ATTACHING IT.
ATTACH,LBUTILS.
.ENDIF,NOLIBUT.
RETURN,LXXX.
.IF,$LIB1$.NE.$NO$,DOLIB1.
.IF,.NOT.FILE(LIB1,PM).REVERT,ABORT.LIBRARY LIB1 IS NOT ATTACHED.
LDSET,LIB1.
.ENDIF,DOLIB1.
.IF,$LIB2$.NE.$NO$,DOLIB2.
.IF,.NOT.FILE(LIB2,PM).REVERT,ABORT.LIBRARY LIB2 IS NOT ATTACHED.
LDSET,LIB2.
.ENDIF,DOLIB2.
LDSET,LIB=LBUTILS.
LDSET,LIB=SYMLIB.
LOAD,OBJ.
NOGO,LXXX.
RETURN,PGM.
PURGE,PGM/NA.
DEFINE,PGM.
REWIND,LXXX.
COPYBF,LXXX,PGM.
RETURN,LXXX.
REVERT,NOLIST.
EXIT.
REVERT,ABORT.ERROR DOING LINKPGM.
*WEOR
