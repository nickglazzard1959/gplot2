      INTEGER FUNCTION STREAD( FIN, AOUT, WITHLF )
C-------------------------------------------------------(STRING)--------
C READ A LINE OF TEXT FROM FIN AND RETURN IT AS ASCII CODES IN AOUT
C WITH A LINE FEED (KNL) AT THE END IF WITHLF, OTHERWISE NOT.
C RETURN THE NUMBER OF CHARACTERS IN THE LINE, INCLUDING THE KNL.
C ON EOF, RETURN KSTEOF. ON ERROR, RETURN KSTERR.
C-----------------------------------------------------------------------
      IMPLICIT CHARACTER*1 (A-Z)
      INCLUDE 'stringh.cmn'
      INTEGER FIN
      INTEGER AOUT(KSTDIM)
      LOGICAL WITHLF
C----
      CHARACTER*(KCMAX)INLINE
      INTEGER I, INLEN, NCODES
C
      INTEGER STCTOA
C
C---- READ A LINE OF 6/12 DISPLAY CODE CHARACTERS.
C     READ(FIN, 100, END=1, ERR=2)(INLINE(I:I),I=1,160)
C100  FORMAT(160A1)
      READ(FIN, 100, END=1, ERR=2)INLINE
 100  FORMAT(A)
C---- FIND THE LAST NON-SPACE CHARACTER FOR INPUT STRING LENGTH.
      INLEN = 0
      DO 3 I=KCMAX,1,-1
         IF( INLINE(I:I) .NE. ' ' )THEN
            INLEN = I
            GOTO 4
         ENDIF
 3    CONTINUE
 4    CONTINUE
C---- CONVERT THE TRIMMED CHARACTERS TO ASCII CODES WITH 6/12 RULES.
      NCODES = STCTOA(INLINE(1:INLEN), AOUT)
C---- ADD A KNL END-OF-LINE MARKER AT THE END. THIS IS ALWAYS SAFE.
      IF( WITHLF )THEN
         NCODES = NCODES + 1
         AOUT(NCODES) = KNL
      ENDIF
C---- TERMINATE THE MODIFIED STRING. THIS IS ALWAYS SAFE TOO.
      AOUT(NCODES+1) = KSTEND
C---- RETURN NORMALLY.
      STREAD = NCODES
      RETURN
C---- END OF FILE.
 1    CONTINUE
      STREAD = KSTEOF
      RETURN
C---- READ ERROR.
 2    CONTINUE
      STREAD = KSTERR
      RETURN
      END
