          IDENT  FILESZI
          ENTRY  FILESZI
*
* INTEGER FUNCTION FILESZI( BOOLEAN BNAME )
*---------------------------------------------------(NOS I/F)-----------
*   RETURN THE SIZE OF A FILE IN PRUS (64 WORD SECTORS).
*-----------------------------------------------------------------------
*
 FILESZI  BSS    1           ENTRY.
          SA1    X1          X1 = BNAME
          MX2    42          X2 = 1S 59 TO 18, 02 17 TO 0
          BX7    X2*X1       X7 = NAME 59 TO 18
          SX2    5           X2 = 5 = REQ BLK LEN
          SB2    12          B2 = SHIFT FOR REQ BLK LEN
          LX2    B2,X2       X2 = 5 << 12
          SX0    1           X0 = 1
          BX7    X7+X2       X7 = NAME, REQ BLK LEN
          BX7    X7+X0       X7 = NAME, REQ BLK LEN, C
          SA7    REQBLK      REQBLK[0] = NAME, REQ BLK LEN, C
          FILINFO REQBLK     MACRO.
          SB2    3           B2 = OFFSET TO SIZE.
          SA3    B2+REQBLK   X3 = LEN, 0, RAND ADDR
          SB2    36          B2 = SHIFT TO LEN
          AX3    B2,X3       X3 = X3 >> 36
          BX6    X3          X6 = LEN
          EQ     FILESZI     RETURN
 REQBLK   BSS    5           REQUEST BLOCK SPACE
          END
