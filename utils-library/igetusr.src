          IDENT IGETUSR
          ENTRY IGETUSR
*
* INTEGER FUNCTION IGETUSR( CHARACTER*7 USN )
*----------------------------------------------------(NOS I/F)----------
*   OBTAIN THE USERNUM. RETURN THE NUMBER OF CHARACTERS THEREIN.
*-----------------------------------------------------------------------
*
IGETUSR   BSS    1           JUMP WHENCE WE CAME
          SX0    X1          SAVE ADDRESS OF ARG1 IN X0
          USERNUM RESULT     MACRO. GET USERNAME (UP TO 7 CHARS)
          SA5    RESULT      GET VALUE OF RESULT IN X5.
          BX6    X5          TRANSFER TO X6 SO WE CAN STORE IT.
          SA6    X0          STORE IT IN OUTPUT ARGUMENT USN.
* COUNT OCCUPIED CHARACTERS. 0 IN CHARACTER IS END OF STRING.
* INITIALIZE LOOP.
          SX4    77B         X4 = 77B  SINGLE CHARACTER MASK.
          SB2    7           B2 = 7    MAXIMUM NUMBER OF POSSIBLE CHARAC
          SB3    54D         B3 = 54   LEFT SHIFT AMOUNT TO REACH FIRST
* LOOP, CHECKING EACH CHARACTER FROM LEFTMOST FOR 0.
 LOOP     LX3    B3,X4       X3 = X4 LSHIFT B3  (CURRENT CHARACTER MASK)
          BX1    X3*X5       X1 = USERNUM .AND. MASK (EXTRACT CURRENT CH
          ZR     X1,EOS      FOUND 0 TERMINATING CHARACTER
          SB2    B2-1        B2 -= 1 (DECREMENT CHARACTERS LEFT TO EXAMI
          ZR     B2,EOS      EXAMINED ALL 7 CHARACTERS
          SB3    B3-6        REDUCE MASK SHIFT BY 6
          EQ     LOOP        LOOK AT NEXT CHARACTER.
* END OF LOOP. CONVERT CHARACTERS LEFT TO EXAMINE TO CHARACTER COUNT.
 EOS      SX6    7           X6 = 7
          SX7    B2          X7 = B2 CHARACTERS LEFT TO PROCESS.
          IX6    X6-X7       X6 = 7 - CHARACTERS LEFT TO PROCESS.
          EQ     IGETUSR     RETURN
 RESULT   BSS    1           MACRO RESULT BUFFER
          END
