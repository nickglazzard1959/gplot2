      SUBROUTINE DFX216(ITEXT,AVAILX,AVAILY,X,Y,H,MODE)
C
C    **INTERNAL SERVICE ROUTINE - MODE 0 EQUIVALENT OF STRFIT**
C
C    THIS SUBROUTINE RETURNS THE OFFSETS AND HEIGHT TO ENABLE THE
C    INPUT TEXT STRING TO BE MAXIMALLY FITTED WITHIN THE BOX,
C    DEFINED BY ITS AVAILABLE X AND Y EXENTS.
C    THE POSITIONING IS CONTROLLED BY MODE.
C
C    MODE = MN
C              DIGITS M,N RELATE TO HORIZONTAL,VERTICAL RESPECTIVELY
C              0 - CENTRE
C              1 - LEFT/BOTTOM
C              2 - RIGHT/TOP
C
C    I.E. EXPLICITLY -
C
C    MODE = 00    TEXT WILL BE CENTRED IN BOX
C           01    TEXT CENTRED HORIZONTALLY AT BOTTOM
C           02    TEXT CENTRED HORIZONTALLY FLUSH AT TOP
C           10    TEXT LEFT JUSTIFIED CENTRED IN HEIGHT
C           11    TEXT LEFT JUSTIFIED AT BOTTOM
C           12    TEXT LEFT JUSTIFIED FLUSH TO TOP
C           20    TEXT RIGHT JUSTIFIED CENTRED IN HEIGHT
C           21    TEXT RIGHT JUSTIFIED AT BOTTOM
C           22    TEXT RIGHT JUSTIFIED FLUSH TO TOP
C
C    NOTE - TRAILING BASELINE BLANKS ARE IGNORED AND ALLOWANCE IS
C           MADE FOR ANY INTERCHARACTER SPACING (FOR THE LAST BASELINE
C           CHARACTER) SO THE RETURNED LENGTH WILL PHYSICALLY CLEAR
C           THE PLOTTED STRING
C
      CHARACTER*(*) ITEXT
      INCLUDE 'dfxc01.cmn'
      INCLUDE 'dfxc01s.cmn'
      INCLUDE 'dfxc00.cmn'
      INCLUDE 'dfxc00s.cmn'
      INCLUDE 'dfxc05.cmn'
      INCLUDE 'dfxc12.cmn'
      INCLUDE 'dfxc23.cmn'
      IF ((AVAILX.LE.0.0).OR.(AVAILY.LE.0.0)) GO TO 100
      LENGTH = .TRUE.
      CALL DFX214(ITEXT)
      LENGTH = .FALSE.
      Z = XPASS/HT
C    Z IS LENGTH AT UNIT HEIGHT
      XPASS = 100.0
      X = 0.0
      Y = 0.0
      IF (Z.LE.0.0) GO TO 999
C    IN CASE NULL STRING - RETURN WITH SENSIBLE PARAMETERS
      IF( ABS(YFIT1 - YFIT2) .LE. 0.0001 )THEN
         YFIT2 = YFIT1 + 1.0
      ENDIF
      YMIN = YFIT1/HT
      YMAX = YFIT2/HT
C    GET MAXIMUM HEIGHT -
C    ENSURING X- AND Y- AVAILABLE EXTENTS NOT EXCEEDED
      H = MIN((AVAILX/Z),(AVAILY/(YMAX-YMIN)))
      Z = Z*H
      YMAX = YMAX*H
      YMIN = YMIN*H
C
C    NOW PROCESS MODE AND DETERMINE APPROPRIATE OFFSETS
C
      MX = MODE/10
      MY = MODE - MX*10
C
C    HORIZONTAL ALIGNMENT
C
C    DEFAULT TO LEFT JUSTIFY (MX=1)
      X = 0.0
C    CENTRE HORIZONTALLY (MX=0)
      IF (MX.EQ.0) X = .5*(AVAILX - Z)
C    RIGHT JUSTIFY (MX=2)
      IF (MX.EQ.2) X = AVAILX - Z
C
C    VERTICAL ALIGNMENT
C
C    DEFAULT TO BOTTOM (MY=1)
      Y = 0.0
C    CENTRE VERTICALLY (MY=0)
      IF (MY.EQ.0) Y = .5*(AVAILY-(YMAX-YMIN)) - YMIN
C    FLUSH TO TOP (MY=2)
      IF (MY.EQ.2) Y = AVAILY - YMAX
  999 RETURN
  100 IF (ICHECK.GT.0) WRITE(ERRREC,101) ROUTIN,AVAILX,AVAILY
  101 FORMAT(1H0,'**DIMFILM WARNING**  ',A,' SPECIFIED INVALID VALUE(S)
     1FOR AVAILABLE SPACE'/1H ,21X,'AVAILABLE X = ',1PE16.8,
     2'   AVAILABLE Y = ',1PE16.8/1H ,21X,'CALL IGNORED')
      CALL DFX130(0)
      GO TO 999
      END
