C
C PROCEDURE TO DRAW AN ALGORITHMICALLY DEFINED
C PLANT USING A LINDENMAYER SYSTEM.
C USAGE:
C  OBEY OBPLANT <ITERATIONS>
C   ITERATIONS BETWEEN 5 AND 8 SEEMS REASONABLE.
C
C INITIALISE GPLOT STATE.
RESET
C
C SET THE COLOUR OF THE PLANT.
CSG GENERAL
COL 0.05 0.6 0.03
C
C DEFINE THE LINDENMAYER SYSTEM FOR THE PLANT.
STRING 1 X
STRING 2 F:FF
STRING 3 X:F-[[X]+X]+F[+FX]-X 
STO 3 90
C
C DRAW THE PLANT
LSYSTEM 2 $1 22.5
C
C SETUP TO ADD ANNOTATION
BOUNDS 0 1 0 1
MOVE 0.25 0.25
CSG TEXT
COL 0 0 0
C
C DEMONSTRATE CTEXT CENTRED TEXT.
CTEXT 0.4 "L*LINDENMAYER *UP*LLANT"
MOVE 0.25 0.185
CTEXT 0.35 "X, F:FF, X:F-[[X]+X]+F[+FX]-X, 22.5*L DEGREES, $1 ITERATIONS."
C
C DEMONSTRATE ROTATED TEXT VIA THE RPN EVALUATOR.
STRING 5 "GPLOT/DIMFILM D*LEMONSTRATION AND TEST SCRIPT"
STRING 6 "  DIMFILM 1972-95, GPLOT 2013-"
EVAL "0.98,0.05,M,90,TA,5,T,0.98,0.6,M,6,T"
C
C ADD THE GPLOT VERSION INFO, DRAWING CENTRED TEXT
C VIA A STORED PROCEDURE.
VERSION
LOADP 1 ECTEXT
EVAL @1 "0.25,0.125,0.35,9"
