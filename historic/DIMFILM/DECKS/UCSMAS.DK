*DECK UCSMAS F
      SUBROUTINE DFXMAS
      IMPLICIT INTEGER (A-Z)
      CHARACTER*8 JOBNAM
      CHARACTER*8 JID
      CHARACTER*20 JNAM
      CHARACTER*1 JNAM1
*CALL,DFXC05
C------------------------------------------------------
C
C   THREE ENTRY POINTS -
C
C     DFXMNS - RETURN ACCOUNT NUMBER/JOBNAME
C     DFXMUS - RETURN JOBNAME (WOULD PREFER JOB COMMENT FIELD)
C     DFXMJS - RETURN JOB SEQUENCE NUMBER
C     DFXMSS - RETURN STAGED DISK DATASET JOB PARAMETER (SD)
C
C
C
C       DIMFILM MACHINE DEPENDENT INTERFACE - UNICOS VERSION
C       VERSION 1.0        22/05/90        JOHN GILBERT,ULCC
C
C------------------------------------------------------
C
C   ENTRY DFXMNS - RETURN ACCOUNT NUMBER/JOBNAME
C
C   CHARACTER*8 JOBNAM
C
C   CALLED AS :
C                  CALL DFXMNS(JOBNAM)
C
C   ON AMDAHL JOBNAME=ACCOUNT NUMBER+OPTIONAL LETTER
C   ON UNICOS THE UNAME IS EQUIVALENT TO ACCOUNT NUMBER
C   AT PRESENT THERE IS NO EQUIVALENT TO JOBNAME (ON COS
C   USED FIRST LETTER OF CRAY JN PARAMETER)
C   FOR NQS JOBS MAY BE ABLE TO GET FIRST LETTER OF NAME
C   FROM NQS-R PARAMETER - THIS MAY ALSO BE USED
C   IN PLACE OF COMMENT FIELD
C
C   DFXMCS IS USED TO PROCESS UNICOS COMMANDS AND
C   ACCESS THE RETURNED STRING
C   SYSCOD USED TO PASS ROUTINE TO DFXMCS FOR DIAGNOSTICS
C
      ENTRY DFXMNS(JOBNAM)
      SYSCOD = 'DFXMNS'
      CALL DFXMCS('logname',JOBNAM,LRET)
C    FOR CONSISTENCY WITH OTHER SYSTEMS FORCE LOGAME TO UPPER CASE
C    (ULTIMATELY THIS COULD BE DONE ON THE DICOMED JUST FOR THE
C     MAIN BANNER HEADING - FOR NOW DO IT HERE)
      DO 1 I=1,8
      IC = ICHAR(JOBNAM(I:I))
C    FORCE a-z to A-Z
      IF ((IC.GE.97).AND.(IC.LE.122)) THEN
            IC = IC - 32
            JOBNAM(I:I) = CHAR(IC)
      ENDIF
    1 CONTINUE
      IF (.NOT.SYSIA) THEN
C    IF BATCH -
C    AND ADD FIRST LETTER OF JOB NAME PARAMETER (SEE DFXMUS, BELOW)
            CALL DFXMCS('printenv QSUB_REQNAME',JNAM1,LRET)
            IF (JOBNAM(8:8).EQ.' ') JOBNAM(8:8) = JNAM1(1:1)
      ENDIF
      RETURN
C------------------------------------------------------
C
C   ENTRY DFXMUS - RETURN USER (I.E. JNAM) PARAMETER (IF ANY)
C
C   CHARACTER*20 JNAM
C
C   CALLED AS :
C                  CALL DFXMUS(JNAM)
C
C   IDEALLY SHOULD RETURN COMMENT FIELD/NAME FROM NQS IF BATCH.
C   IN ABSENCE OF THIS INFORMATION USE JOBNAME PARAMETER
C
C*****TEMPORARILY USE USER NAME****
      ENTRY DFXMUS(JNAM)
      SYSCOD = 'DFXMUS'
      IF (SYSIA) THEN
C   IF INTERACTIVE SET AS BLANK
             JNAM = ' '
      ELSE
C   IF BATCH USE JOB NAME
             CALL DFXMCS('printenv QSUB_REQNAME',JNAM,LRET)
      ENDIF
      RETURN
C------------------------------------------------------
C
C   ENTRY DFXMJS - RETURN JOB IDENTIFIER (C.F. JSQ ON COS)
C
C   CHARACTER*8 JID
C
C   CALLED AS :
C                  CALL DFXMJS(JID)
C   IF INTERACTIVE:
C   RETURNS JID IN THE FORM  PIDNNNNN WHERE N=DIGIT
C   IF BATCH:
C   RETURNS JID AS QSUB REQUEST ID (STRIPS HOST)
C   (AND RETURNS AS nqsNNNNN)
C
      ENTRY DFXMJS(JID)
      SYSCOD = 'DFXMJS'
      IF (SYSIA) THEN
C    INTERACTIVE, SO USE PID
            IPID = GETPID()
            WRITE(JID,1000) IPID
 1000 FORMAT('PID',I5)
            DO 2 I=4,8
    2       IF (JID(I:I).EQ.' ') JID(I:I) = '0'
      ELSE
C    BATCH - SO GET QSUB REQUEST ID
            JID(1:3) = 'nqs'
            CALL DFXMCS('printenv QSUB_REQID',JID(4:),LRET)
            I = INDEX(JID,'.')
C    STRIP TRAILING HOST (OF FORM nnnn.host)
            IF (I.GT.0) JID(I:) = ' '
      ENDIF
      RETURN
C------------------------------------------------------
C
C   ENTRY DFXMSS - RETURN STAGE DATASET STATUS (UNDER COS JCL SD)
C
C   FINDS OUT IF SD HAS BEEN SET
C   RETURNS SINGLE INTEGER PARAMETER ISD :
C                 ISD  =  0 IF SD NOT SET
C                 ISD  =  1 IF SD SET
C
C   UNDER UNICOS ASSUME SD SET (I.E. AWAIT COMPLETION OF DISPOSE)
C
C   CALLED AS :
C                  CALL DFXMSS(ISD)
C
      ENTRY DFXMSS(ISD)
      ISD = 1
      RETURN
      END
