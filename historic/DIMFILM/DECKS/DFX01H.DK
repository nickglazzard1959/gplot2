*DECK DFX01H F
      SUBROUTINE DFX01H(DEV,HES,HPS,HPE,VES,VPS,VPE,DIAG,IERR)
C
C    DICOMED DEVICE SPECIFIC CONTROL
C    SETS RASTER PARAMETERS FOR SPECIFIED DEVICE (IF ACTIVE)
C    DIAGNOSTICS WILL BE ISSUED UNLESS DIAG = .FALSE.
C    (INVALID HES..VPE ALWAYS REPORTED)
C    IERR WILL RETURN -   0  PERFORMED
C                        -1  DEVICE OPEN, BUT NOT ACTIVE
C                        -2  DEVICE NOT OPEN
C                        +1  INVALID PARAMETERS
C
      INTEGER HES,HPS,HPE,VES,VPS,VPE
      LOGICAL DIAG
      CHARACTER*(*) DEV
      CHARACTER*6 CERR
      CHARACTER*23 CERR2
*CALL,DFXC00
*CALL,DFXC05
*CALL,DFXCD0
*CALL,DFXCP0
*CALL,DFXPAB
*CALL,DFXCAD
*CALL,DFXCBD
      LOGICAL TEST
      TEST(I,J,K) = (I.LT.J).OR.(I.GT.K)
C
C    SET TABLE OF MIN/MAX VALUES
C    NOTE: THESE ARE IN VECTOR (32K) ADDRESSABILITY RELATIVE
C          TO RASTER (4K) SPACE  (C.F. DICOMED BINARY POINT
C                                      OF 3 BITS = FACTOR OF 8)
C          ALSO, POINTS PER ELEMENT OF 16 MAPS TO 0 BEFORE
C                ONWARD TRANSMISSION
C
C    ******CONTRADICTION IN D48 SPECIFICATION - PE OF 16 REQUIRES
C          16 NOT 15 7/8 ELEMENTS SPACING TO AVOID OVERLAP
C          ADDITIONALLY, MIN OF 1 RASTER UNIT ENFORCED***********
C
      DATA MINES,MINPS,MINPE,MAXES,MAXPS,MAXPE/
     1      8   ,  8  ,  1  , 127 ,  63 ,  16/
C
C    FOREGOING MIN/MAX VALUES ARE ALSO CODED INTO FORMAT 31
C
      DATA CERR2/'HORIZONTAL AND VERTICAL'/
C    CHECK STATUS OF DEVICE
      CALL DFX008(DEV,IWSNO)
C    IWSNO WILL BE DEVICE NUMBER OF OPEN DEVICE
      IERR = -2
      IF (IWSNO.LE.0) GO TO 10
      IERR = -1
      IF (.NOT.WSAC(IWSNO)) GO TO 10
      IERR = 0
C    HAVE OPEN AND ACTIVE DEVICE
C
C    CHECK VALIDITY/CONSISTENCY OF PARAMETERS
C
      IF (TEST(HES,MINES,MAXES)) GO TO 30
      IF (TEST(HPS,MINPS,MAXPS)) GO TO 30
      IF (TEST(HPE,MINPE,MAXPE)) GO TO 30
      IF (TEST(VES,MINES,MAXES)) GO TO 30
      IF (TEST(VPS,MINPS,MAXPS)) GO TO 30
      IF (TEST(VPE,MINPE,MAXPE)) GO TO 30
      I1 = 0
      IF (HES.LT.(HPE*HPS)) THEN
             I1 = 1
             I2 = 10
      ENDIF
      IF (VES.LT.(VPE*VPS)) THEN
             I2 = 23
             IF (I1.EQ.0) I1 = 16
      ENDIF
      IF (I1.NE.0) THEN
           IF (ICHECK.GT.0) WRITE(ERRREC,12) ROUTIN,CERR2(I1:I2),DEV,
     1           HES,HPS,HPE,VES,VPS,VPE
   12 FORMAT(1H0,'**DIMFILM WARNING** ',A,' DETECTED OVERLAPPING ELEMENT
     1S IN ',A,' FOR DEVICE ',A/
     11H ,21X,'PARAMETERS WERE ',6I6)
*CALL,LINOUT0
      ENDIF
      IPAR(1) = HPE
      IPAR(2) = VPE
      IPAR(3) = HES
      IPAR(4) = HPS
      IPAR(5) = VES
      IPAR(6) = VPS
C    SET DEVICE FOR DFX000
      NWS = IWSNO
C    NOW SEND PARAMETERS TO DEVICE
      CALL DFX000(-2005,ZDUMMY,ZDUMMY,ZDUMMY,NDUMMY)
   99 RETURN
   10 IF (DIAG) THEN
          CERR = 'ACTIVE'
          IF (IERR.EQ.-2) CERR = 'OPEN'
          IF (ICHECK.GT.0) WRITE(ERRREC,11) ROUTIN,DEV,CERR
*CALL,LINOUT0
   11 FORMAT(1H0,'**DIMFILM WARNING** ',A,' REFERENCED WHEN DEVICE ',
     1   A,' WAS NOT ',A,'; CALL IGNORED')
      ENDIF
      GO TO 99
   30 IF (ICHECK.GT.0) WRITE(ERRREC,31) ROUTIN,HES,HPS,HPE,
     1                                  VES,VPS,VPE
*CALL,LINOUT0
   31 FORMAT(1H0,'**DIMFILM WARNING** ',A,' DETECTED ONE OR MORE OUT OF
     1RANGE PARAMETERS, THESE WERE -'/1H ,21X,I6,' (8,127),',I6,
     2 ' (8,63),',I6,' (1,16),',I6,' (8,127),',I6,' (8,63),',
     3 I6,' (1,16) - ALL RANGES INCLUSIVE'/
     4 1H ,21X,'CALL IGNORED')
      IERR = +1
      GO TO 99
      END
