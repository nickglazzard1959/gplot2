*DECK DFX020 F
      INTEGER FUNCTION DFX020(Z)
C    RETURNS IN DFX020 (INTEGER) NUMBER OF LEADING DIGITS
C           (INCLUDING SIGN) IN Z(REAL)
C    THIS IS USED TO DETERMINE POSITIONING OF NUMERIC VALUES
C    E.G. BY GRAPH ANNOTATION ROUTINES
C
C    CAUTION - IF OUTPUT IS TO BE BY REAL FORMAT THEN ROUNDING
C              OF SIGNIFICANT DIGITS AFTER DECIMAL POINT MAY
C              RESULT IN INCREASE OF LEADING DIGITS
C              E.G. 99.996 OUTPUT AS F10.2
C              TO AVOID THIS SCALE VALUE AND ROUND TO INTEGER
C              BY FACTOR 10**(DECIMAL PLACES), FLOAT VALUE
C              THEN -
C                     FOR LEADING DIGITS DECREMENT RETURN
C                     FROM DFX020 BY THIS NUMBER OF DECIMAL PLACES
C                  OR
C                     FOR TOTAL REQUIRED DIGITS INCREMENT RETURN BY
C                     ONE TO ALLOW FOR DECIMAL POINT
      IDIG = 1
      AZ = ABS(Z)
      IF (AZ.GE.10.) THEN
           IDIG = 1 + INT(ALOG10(AZ))
C    DUE TO ROUNDING THIS MAY SOMETIMES BE 1 OUT
C    IT IS THEREFORE USED AS A STARTING POINT
    1      TENTOJ = 10**(IDIG-1)
           TENTOI = 10*TENTOJ
C    WANT AZ.LT.TENTOI  AND AZ.GE.TENTOJ
           IF (AZ.GE.TENTOI) THEN
               IDIG = IDIG + 1
               GO TO 1
           ENDIF
           IF (AZ.LT.TENTOJ) THEN
               IDIG = IDIG - 1
               GO TO 1
           ENDIF
      ENDIF
C    ALLOW FOR SIGN
      IF (Z.LT.0.0) IDIG = IDIG + 1
      DFX020 = IDIG
      RETURN
      END
