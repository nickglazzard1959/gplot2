*DECK PIECHT F
      SUBROUTINE PIECHT(R1,CAPTON,PERCNT,N1,PERS)
C    R1 RADIUS OF CIRCLE, CENTRED AT CENTRE OF CURRENT WINDOW
C    NOTE...CIRCLE MUST FIT WITHIN GRAPHICAL AREA THAT IS .85 OF WINDOW
C    CAPTON(N1)  CHARACTER ARRAY HOLDS  CAPTION -THIS WILL BE
C    PLOTTED WITH TRAILING BLANKS DELETED
C    PERCNT(N1) IS REAL ARRAY THE N TH ELEMENT BEING THE PERCENTAGE
C    CORRESPONDING TO THE N TH SECTOR
C    N1 IS INTEGER NUMBER OF SECTORS TO BE PLOTTED
C    THE PIE DIAGRAM WILL BE CENTRED IN THE CURRENT WINDOW
C    IF PERCENTAGES ARE REQUIRED TO FOLLOW CAPTION PLOT THEN PERS
C    SHOULD HAVE AN ABSOLUTE VALUE OF 2, OTHERWISE 1
C    IF PERS IS NEGATIVE ACTUAL VALUES ARE INPUT
C    IF PERS POSITIVE PERCENTAGES ARE INPUT
C    (NOTE.. PERS IS INTEGER)
C    IN THIS CASE NO MORE THAN 15 CHARACTERS SHOULD BE IN THE CAPTION,
C    THE LAST OF WHICH SHOULD BE BLANK
C    IF PERCENTS ADDED TO CAPTION SOME ADJUSTMENT MAY BE NECESSARY
C    TO THE COMPUTATION OF SIZE
      LOGICAL PERC
      INTEGER PERS
      CHARACTER*(*) CAPTON(N1)
      REAL PERCNT(N1)
      LOGICAL TOT
*CALL,DFXC06
*CALL,DFXC02
*CALL,DFXC00
*CALL,DFXC01
*CALL,DFXC03
*CALL,DFXC05A
      ROUTIN = 'PIECHT'
*CALL,RIN
      PERC = .FALSE.
      IF (IABS(PERS).GT.1) PERC = .TRUE.
      N = N1
      SUM = 0.0
      DO 1 I=1,N
    1 SUM = SUM + PERCNT(I)
      TOT = .TRUE.
      PERTOT = 1.0
      IF (PERS.GT.0) GO TO 20
      PERTOT = 100./SUM
      GO TO 21
   20 IF (SUM.GT.100.1) GO TO 10
      IF (SUM.LT.99.9) TOT = .FALSE.
C    NOTE - IF SUM OF PERCENTAGES EXCEEDS 100.1 PROGRAM WILL RETURN
C    AND A MESSAGE PRINTED IF ICHECK .GT. 0
C    IF LESS THAN 99.9 AN UNLABELLED SECTOR WILL BE DRAWN CORRESPONDING
C    TO THE EMPTY AREA
   21 R = R1
      D = 2.*R
      IF (D.LE.0.0.OR.D.GT.(XTB2-XTB1)*DC1.OR.D.GT.(YTB2-YTB1)*DC1)
     1                   GO TO 11
      X1 = .5*(XTB2 + XTB1)
      Y1 = .5*(YTB2 + YTB1)
      CALL DFX500(X1,Y1,R,0.,360.,.FALSE.)
C    PARAMETER 5 IGNORED BY DFX500 - DEGREES USED HERE DESCRIPTIVELY
C    LAST PARAMETER (LOGICAL) INDICATES CALL OF DFX500 IS WHOLE CIRCLE
      CALL DFX300
      CALL DFX134(X1,Y1)
      CAPANG = 0.0
      RLOW = .25*R
      RUP = .875*R
      RLEN = RUP - RLOW
      RS = R*.0625
      CNT2PI = .01*TWOPI
      DO 2 I=1,N
      ANGLE = CNT2PI*PERCNT(I)*PERTOT
      SEMI = .5*ANGLE
      CAPANG = CAPANG + SEMI
      CALL DFX213(CAPTON(I),NO,ZS)
      IF (NO.LE.0) GO TO 3
      IF (PERC) NO = NO + 5
      SSEMI = AMIN1(SEMI,1.0)
C    NEED ONLY ENSURE SEMI SUCH THAT ARG1 < RS ACCOUNTED FOR
      ARG1 = RLOW*TAN(SSEMI)*1.5
      ARG2 = RLEN/ZS
      SIZE = AMIN1(ARG1,ARG2)
      IF (PERC) NO = NO - 5
      SIZE = AMIN1(SIZE,RS)
      XL = RLOW
      F = +.5
      CAPA2 = CAPANG
      IF ((CAPANG.LE.PIBY2).OR.(CAPANG.GT.3.*PIBY2)) GO TO 6
      F = -.5
      XL = RUP
      CAPA2 = CAPA2 + PI
      IF (CAPA2.GE.TWOPI) CAPA2 = CAPA2 - TWOPI
    6 C = COS(CAPANG)
      S = SIN(CAPANG)
      X = XL*C + SIZE*S*F
      Y = XL*S - SIZE*C*F
      CALL DFX110(X,Y)
      HT = SIZE
      CANGL = COS(CAPA2)
      SANGL = SIN(CAPA2)
      CALL DFX214(CAPTON(I)(1:NO))
      IF (.NOT.PERC) GO TO 3
      PER = PERCNT(I)*PERTOT
C  PERFORM EQUIVALENT OF SYMCON
      SYMCO = .TRUE.
      CALL DFX204(1,PER,IDUMMY,'(F4.1,''*(5'')')
C  PERFORM EQUIVALENT OF ENSYMC
      SYMCO = .FALSE.
    3 CALL DFX110(0.,0.)
      CAPANG = CAPANG + SEMI
      X = R*COS(CAPANG)
      Y = R*SIN(CAPANG)
      CALL DFX106(X,Y)
    2 CONTINUE
      IF (TOT) GO TO 4
      CALL DFX110(0.,0.)
      CALL DFX106(R,0.)
    4 CALL DFX301
*CALL,IMM
   99 CONTINUE
*CALL,ROUT
      RETURN
   10 IF (ICHECK.GT.0) WRITE(ERRREC,12)
*CALL,LINOUT0
   12 FORMAT(1H0,'**DIMFILM WARNING**  PIECHT CALLED WITH PERCENTAGES TO
     1TALLING IN EXCESS OF 100.1'/1H ,21X,'CALL IGNORED')
      GO TO 99
   11 IF (ICHECK.GT.0) WRITE(ERRREC,13)
*CALL,LINOUT0
   13 FORMAT(1H0,'**DIMFILM WARNING**  PIECHT CALLED WITH SPECIFIED RADI
     1US EXCEEDING HALF LEAST DIMENSION OF CURRENT GRAPHING AREA'/1H ,
     2 21X,'CALL IGNORED')
      GO TO 99
      END
