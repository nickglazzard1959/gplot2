*DECK DSET F
      SUBROUTINE DSET(DEV,KEY1)
C
C    DICOMED DEVICE SPECIFIC CONTROL
C    SETS MISCELLANEOUS FUNCTIONS UNDER CONTROL OF KEYWORD
C
C    USED FOR ROTATIONS AND SINGLE/DOUBLE FRAMING
C
C     NOTE: SINGLE/DOUBLE FRAMING BECOME EFFECTIVE
C           ONLY AFTER NEXT ACTUAL FRAME ADVANCE
C
C    FOR DEV = 'ALL' FOR ALL ACTIVE DICOMED DEVICES
C
      CHARACTER*(*) DEV,KEY1
      PARAMETER (NDEVS=5)
      CHARACTER*6 DDEVS(0:NDEVS),CERR
      CHARACTER*9 SKEY1(6)
      CHARACTER*9 CAMB
      CHARACTER*3 CCC
      INTEGER NROT(5,4)
*CALL,DFXC00
*CALL,DFXC05A
      DATA DDEVS/'ALL','D35','D16','D35P','D35C','D16C'/
C                  0     1     2     3      4      5
      DATA SKEY1/'COMIC','CINE','LANDSCAPE','PORTRAIT',
     1           'SINGLE','DOUBLE'/
      DATA NKEY1,LKEY1/6,9/
C    SKEY1 IS ARRAY OF NKEY1 ENTRIES OF LENGTH LKEY1 CHARS FOR FIRST KEY
      DATA NROT/  5*0  ,  5*1 , 0,1,0,0,1 , 1,0,1,1,0 /
C                COMIC   CINE   LANDSCAPE   PORTRAIT
C    NROT HOLDS ROTATION VALUE FOR CORRESPONDING DEVICE/KEYS
      ROUTIN = 'DSET'
*CALL,RIN
      DO 1 I=0,NDEVS
      IF (DEV.EQ.DDEVS(I)) GO TO 2
    1 CONTINUE
C    NO MATCH FOR DEVICE
      IF (ICHECK.GT.0) WRITE(ERRREC,10) DEV
*CALL,LINOUT0
   10 FORMAT(1H0,'**DIMFILM WARNING** DSET REFERENCED WITH INVALID DEVIC
     1E ',A,'; CALL IGNORED')
      GO TO 99
    2 CONTINUE
      L = LEN(KEY1)
C    CHECK KEY1 AGAINST VALID KEYS (MUST BE UNAMBIGUOUS)
      NL = MIN(L,LKEY1)
      JKEY1 = 0
      DO 4 J=1,NKEY1
      IF (KEY1.NE.SKEY1(J)(1:NL)) GO TO 4
C    HAVE MATCH - MUST BE ONLY ONE
      IF (JKEY1.NE.0) THEN
          CAMB= 'AMBIGUOUS'
          ICAMB = 9
          GO TO 12
      ELSE
          JKEY1 = J
      ENDIF
    4 CONTINUE
      CAMB= 'INVALID'
      ICAMB = 7
      IF (JKEY1.EQ.0) GO TO 12
C    MUST CHECK FOR TYPE OF OPERATION
C    JKEY1 = 1,4 IS ORIENTATION
C            5,6 IS SINGLE/DOUBLE FRAMING
      IF (JKEY1.LE.4) THEN
C    HERE FOR ORIENTATION
C    COMIC HAS PICTURE BASE LONGITUDINAL W.R.T. FILM,
C    CINE HAS IMAGE TRANSVERSE ACROSS FILM
C    RELATION FOR LANDSCAPE/PORTRAIT (I.E. ASPECT RATIO) IS
C
C    DEVICE:        D35      D16     D35P     D35C    D16C
C                   (1)      (2)     (3)      (4)     (5)
C
C    ASPECT
C    RATIO
C
C    LANDSCAPE     COMIC    CINE    COMIC    COMIC    CINE
C
C    PORTRAIT      CINE     COMIC   CINE     CINE     COMIC
C
           ICODE = -2003
C    N.B. FOR LANDSCAPE/PORTRAIT DEVICES 2/5 (D16/D16C) DIFFER
      ELSE
C    MUST HAVE SINGLE/DOUBLE FRAMING
           ICODE = -2008
           NARG = JKEY1 - 5
C            (0 = SINGLE FRAME, 1 = DOUBLE FRAME)
      ENDIF
      IF (I.EQ.0) THEN
          JERR = 0
          DO 3 J=1,NDEVS
          IF (JKEY1.LE.4) NARG = NROT(J,JKEY1)
          CALL DFX01I(DDEVS(J),ICODE,NARG,.FALSE.,IERR)
    3     JERR = MAX(JERR,IERR)
          IF (JERR.EQ.0) GO TO 99
          CERR = 'ACTIVE'
          IF (JERR.EQ.-2) CERR = 'OPEN'
          IF (ICHECK.GT.0) WRITE(ERRREC,11) CERR
*CALL,LINOUT0
   11 FORMAT(1H0,'**DIMFILM WARNING** DSET REFERENCED FOR ALL RELEVANT D
     1EVICES FOUND NONE ',A,'; CALL IGNORED')
      ELSE
          IF (JKEY1.LE.4) NARG = NROT(I,JKEY1)
          CALL DFX01I(DEV,ICODE,NARG,.TRUE.,IERR)
      ENDIF
   99 CONTINUE
*CALL,ROUT
      RETURN
   12 NL = MIN(L,20)
      CCC = ' '
      IF (L.GT.20) CCC = '...'
      IF (ICHECK.GT.0) WRITE(ERRREC,13) CAMB(1:ICAMB),KEY1(1:NL),CCC
*CALL,LINOUT0
   13 FORMAT(1H0,'**DIMFILM WARNING** DSET REFERENCED WITH ',A,' KEYWORD
     1 = ',A,A/1H ,21X,'CALL IGNORED')
      GO TO 99
      END
