*DECK MVSMS2 F
DFXMS2   TITLE 'DFXMS2 - CURRENT TIME ROUTINE FOR DIMFILM'
DFXMS2   CSECT
***********************************************************************
*                                                                     *
*      TIME:                                                          *
*                                                                     *
*        RETURNS CURRENT TIME TO CALLER AS 8-BYTE FIELD OF FORM:     *
*                                                                     *
*                      HH.MM.SS                                       *
*                                                                     *
*        ***** WRITTEN BY DAVID CRICHTON, AMDAHL(UK), 11 MAY 1980*
*                                                                     *
***********************************************************************
*
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
F0       EQU   0
*
         STM   R14,R12,12(R13)     SAVE REGISTERS
         LR    R12,R15             GR12 IS BASE REGISTER.
         USING DFXMS2,R12          ESTABLISH ADDRESSABILITY.
         LA    R0,88               SIZE FOR GETMAIN.
         GETMAIN R,LV=(0)          GET DYNAMIC WORK AREA.
         LR    R15,R1              GR15 -> WORK AREA.
         LM    R0,R1,20(R13)       RELOAD PARAMETER REGS.
         ST    R15,8(R13)          RELINK SAVE AREAS.
         ST    R13,4(R15)
         LR    R13,R15             GR13 -> NEW SAVE/WORK AREA.
         USING MAP,R13             ADDRESS.
*
         L     R6,0(R1)            GR6 -> PARM.
         LA    R6,0(R6)            CLEAR HI-BYTE.
*
         TIME  DEC                 GET DATE AND TIME.
*
         ST    R0,TIMEX            STORE TIME.
         UNPK  WORK(7),TIMEX       UNPACK.
*
         MVC   0(2,R6),WORK        SET 'HH'.
         MVC   3(2,R6),WORK+2      SET 'MM'.
         MVC   6(2,R6),WORK+4      SET 'SS'.
         MVI   2(R6),C'.'          SET SEPERATORS:
         MVI   5(R6),C'.'
*
         LR    R1,R13
         L     R13,4(R1)           RESTORE CALLER'S GR13.
         LA    R0,88
         FREEMAIN R,LV=(0),A=(1)
         LM    R14,R12,12(R13)     RESTORE REGISTERS.
         BR    R14                 RETURN.
*
*
         EJECT
*
*        DYNAMIC SAVE/WORK AREA MAP.
*
         SPACE
MAP      DSECT
         DS    18F
DATEX    DS    F
TIMEX    DS    F
WORK     DS    2F
         END
