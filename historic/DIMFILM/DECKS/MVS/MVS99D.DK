*DECK MVS99D F
DFX99D    TITLE  'DYNAMIC ALLOCATION ROUTINES FOR DIMFILM QEC FILE'
          PRINT ON,NOGEN
DFX99D      CSECT
*           DYNAMIC ALLOCATION ROUTINES FOR DIMFILM QEC-FILE
*           THIS VERSION SENDS AN FB 120 FILE TO SYSOUT CLASS 'G'
*                                            M WHEELER.  FEB 1984
*
          ENTRY    DFX99A
          ENTRY    DFX99B
          ENTRY    DFX99C
*
*             ***************  DFX99A  ***************
*             DYNAMICALLY ALLOCATES AND OPENS THE FILE
*
*  CALLING SEQUENCE IS:
*          CALL DFX99A(IFORM,OUTLM,IFAIL)
*
*               IFORM - FOUR BYTE FORM NUMBER
*               IOUTLM- MAX NUMBER OF RECORDS WHICH CAN BE WRITTEN
*               IFAIL - RETURNS 4 BYTE FAILURE CODE ( =0 IF OK)
*
*      FILE STRUCTURE IS :  RECFM=FB,LRECL=120,BLKSIZE=3000
*      ( FILE IS INITIALLY CREATED WITH A SYSOUT CLASS OF 0
*        SO THAT IT WILL VANISH IF DFX99C IS NOT CALLED CORRECTLY )
*
*        ANY ALLOCATION ERROR IS REPORTED IN THE JOB LOG
*
*
DFX99A    STM      14,12,12(13)       SAVE REGS IN FORTRAN SA
          LR       10,15
          USING    DFX99A,10
          LR       11,13
          LA       13,SAVA
          ST       11,4(13)
          ST       13,8(11)
*
          LM       3,5,0(1)           LOAD ADD OF PARAMETERS
*
          MVC      FORM(4),0(3)       MOVE PARM 1 TO FORM
          MVC      OUTLM(3),1(4)      MOVE PARM 2 TO OUTLM
          CLC      0(4,3),BLNK4       IS FORMS CODE BLANK
          BNE      ALOC               IF NOT CARRY ON
          MVI      TA+8,X'80'         ELSE SHORTEN TABLE
*
ALOC      LA       9,0                R9 = 0 (NO ERROR CODE)
          LA       1,PBLOCK           ESTABLISH ADD OF RBP
          DYNALLOC                     ALLOCATE
*
          LTR      15,15              CHECK FOR ERROR CODE
          BZ       OPN                JUMP TO OPEN FILE IF OK
*                    PROCESS ANY ERROR CODE
          ST       15,RETCODE         ELSE GET SYSTEM TO REPORT ERROR
          LA       1,RB1
          ST       1,DFPB
          LA       1,DFPB
          LINK     EP=IKJEFF18
*
          LA       1,RB1+4            POINT TO ERROR CODE
          ST       1,PLIST            STORE ADDRESS IN PARAM LIST
          LA       1,PLIST            POINT TO IT
          CALL     DFXMSA             AND CALL ERROR ROUTINE (FORTRAN)
          L        9,RB1+4            ALSO PUT ERROR CODE INTO R9
          B        RET1               RETURN
*                    ALLOCATION OK
OPN       OPEN     (QFILE,(OUTPUT))   OPEN FILE
*
          MVC      OFLG(1),QFILE+DCBOFLGS-IHADCB   GET OPEN FLAG
          TM       OFLG,X'10'         TEST IF OPEN SUCCEEDED
          BNZ      RET1               BRANCH IF OK
          LA       9,3                ELSE SET ERROR CODE(3)
*
RET1      ST       9,0(5)             PUT ANY ERROR CODE IN IFAIL
*
          L        13,4(13)
          LM       14,12,12(13)       RESTORE REGISTERS
          SLR      15,15
          BR       14
*
*              *************  DFX99B  *************
*                WRITES A RECORD TO THE PLOT-FILE
*
*  CALLING SEQUENCE IS:
*       CALL DFX99B(IBUFF)
*
*                   IBUFF - ARRAY OF LENGTH 120 BYTES
*
*         EQUIVALENT TO :     WRITE(<N>,100) (IBUFF(I),I=1,120)
*                         100 FORMAT(120A1)
*
DFX99B    STM      14,12,12(13)       SAVE REGISTERS
          LR       10,15
          USING    DFX99B,10
          LR       11,13
          LA       13,SAVA
          ST       11,4(13)
          ST       13,8(11)
*
          L        2,0(1)             R2 -> IBUFF
*
          PUT      QFILE,(2)          COPY RECORD
*
*                          TIDY UP
          L        13,4(13)           RESTORE REGISTERS
          LM       14,12,12(13)
          SLR      15,15
          SPACE    2
          BR       14                 RETURN
*
*               **************  DFX99C  ****************
*            CLOSES PLOT-FILE AND CHANGES SYSOUT CLASS TO 'G'
*            (FILE WILL VANISH IF THIS ROUTINE IS NOT CALLED -
*             EG. IF JOB ABORTS BEFORE REACHING THIS ROUTINE)
*   CALLING SEQUENCE IS:
*          CALL DFX99C(IFAIL)
*                      IFAIL - RETURNS FAILURE CODE ( = 0 IF OK)
*
DFX99C    STM      14,12,12(13)       SAVE REGISTERS
          LR       10,15
          USING    DFX99C,10
          LR       11,13
          LA       13,SAVA
          ST       11,4(13)
          ST       13,8(11)
*
          L        3,0(1)             ADDRESS OF IFAIL
*
          CLOSE    (QFILE)            CLOSE FILE
*
          LA       1,DBLOCK
          DYNALLOC                    UNALLOCATE AND SEND
*
          L        9,RB2+4            RETURN ANY FAILURE CODE
          ST       9,0(3)
*
          LTR      9,9                PASS ANY FAILURE CODE TO
          BZ       RETDQ3                      DFXMSA
          LA       1,RB2+4
          ST       1,PLIST
          LA       1,PLIST
          CALL     DFXMSA
*
RETDQ3    L        13,4(13)           RESTORE REGISTERS
          LM       14,12,12(13)
          SLR      15,15
          BR       14                 RETURN
*
*           *************  SVC 99 PARAMETER LISTS *****************
*                          * DYNAMIC ALLOCATION *
          DS       0D
PBLOCK    DC       AL1(S99RBPND),AL3(RB1)
RB1       DC       AL1(20,S99VRBAL),2X'00'
          DC       F'0',A(TA),2F'0'
*
TA        DC       A(TA1,TA2,TA3)
          DC       AL1(S99TUPLN),AL3(TA4)
TA1       DC       AL2(DALDDNAM,1,8)               DD NAME
DDN       DC       CL8'DFXDD099'
TA2       DC       AL2(DALOUTLM,1,3)               OUTPUT LIMIT-
OUTLM     DC       AL3(3000)                          3000 RECORDS
TA3       DC       AL2(DALSYSOU,1,1),AL1(C'0')     SYSOUT(0,
*                                                     (NO PROG NAME)
TA4       DC       AL2(DALSFMNO,1,4)                          ,FORM)
FORM      DC       CL4' '                          MUST BE LAST
* (THIS IS SO THAT FILE WILL VANISH IF DFX99C IS NOT CALLED CORRECTLY)
*          LEAVE DESTINATION SO FILE WILL RETURN TO REMOTE SITE
*
*                 *******************************************
*                          * DYNAMIC UNALLOCATION *
          DS       0D
DBLOCK    DC       AL1(S99RBPND),AL3(RB2)
RB2       DC       AL1(20,S99VRBUN),2X'00'         UNALLOCATE
          DC       F'0',A(TU),2F'0'
TU        DC       A(TU1)
          DC       AL1(S99TUPLN),AL3(TU2)
TU1       DC       AL2(DUNDDNAM,1,8)               DDNAME
          DC       CL8'DFXDD099'
TU2       DC       AL2(DUNOVCLS,1,1),AL1(C'G')     OVERIDE CLASS -'G'
*
*
QFILE     DCB      DDNAME=DFXDD099,DSORG=PS,LRECL=120,RECFM=FB,        +
               BLKSIZE=3000,MACRF=(PM)
*
*                 *******************************************
*                        * ERROR CODE INTERPRETATION *
DFPB      DS       A
          DC       A(RETCODE)
          DC       A(ZERO)
          DC       A(DFFLAGS)
RETCODE   DS       F
ZERO      DC       F'0'
DFFLAGS   DC       B'10000000',X'32'
OFLG      DC       X'00'
PLIST     DS       F
BLNK4     DC       CL4'    '
*
SAVA      DS       20F
          IEFZB4D0
          IEFZB4D2
          DCBD     DSORG=PS,DEVD=DA
          END
