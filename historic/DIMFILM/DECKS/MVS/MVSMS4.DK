*DECK MVSMS4 F
DFXMS4   TITLE 'GET THE JES NUMBER/JOB ID FROM THE SYSTEM'
*
*              D F X M S 4
*
*  A FORTRAN CALLABLE ROUTINE TO RETURN THE 8 CHARACTER JES NUMBER
*
*               MATTHEW WHEELER         MAY 82
*
*  RETURNS AN EIGHT BYTE VALUE TO THE FIRST ARGUMENT.
*  THE RESULT CAN HAVE THREE DIFFERENT FORMS:
*    EG.     'JOB 1234'   FOR BATCH JOB
*            'TSU 1234'   FOR INTERACTIVE JOB (TIME SHARING UNIT)
*            'STC 1234'   FOR STARTED TASK
*
*  CALLING EXAMPLES:
*  FORTRAN H --
*
*      LOGICAL*1 JN(8)
*      CALL DFXMS4 ( JN )
*
*  VS FORTRAN --
*      CHARACTER*8 JN
*      CALL DFXMS4 ( JN )
*
*  PASCAL --
*    PROCEDURE DFXMS4 ( VAR JN : ALFA );  FORTRAN ; ....
         SPACE 4
DFXMS4   CSECT
         SPACE 2
*  SAVE CALLING PROGRAM'S REGISTERS
         STM   14,12,12(13)
         SPACE 2
*  COPY THE JES NUM (8 CHARACTERS) FROM THE SSIB TO THE USERS FIRST
*  (AND PROBABLY ONLY) PARAMETER. ALWAYS 8 CHARACTERS PLANTED.
         L     2,16          CVT
         L     2,0(2)        TCBWRDS
         L     2,4(2)        TCB
         L     2,X'B4'(2)    JSCB
         L     2,X'13C'(2)   SSIB
         SPACE 1
         L     3,0(1)        ADDRESS OF PARAM. 1
         MVC   0(8,3),X'C'(2)    COPY JESNUMBER
         SPACE 2
* RESTORE SAVED REGISTERS
         LM    2,12,28(13)
         L     14,12(13)
         MVI   12(13),X'FF'
         SR    15,15
         SPACE 2
* RETURN TO CALLING PROGRAM
         BR    14
         END
