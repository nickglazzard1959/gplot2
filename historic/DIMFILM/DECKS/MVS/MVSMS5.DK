*DECK MVSMS5 F
DFXMS5   TITLE  'GET TEXT DESCRIBING CURRENT SYSTEM LEVEL'
*
*              D F X M S 5
*
*  A FORTRAN CALLABLE ROUTINE TO RETURN THE 16 CHARACTER FIELD
*  AT  CVT-X'18' WHICH CONTAINS THE SYSTEM LEVEL AND ASSOCIATED INFO
*
*               MATTHEW WHEELER         JUN 82
*
*  RETURNS A SIXTEEN BYTE VALUE TO THE FIRST ARGUMENT.
*
*  EXAMPLES:
*
*  FORTRAN H --
*
*      LOGICAL*1 NM(16)
*      CALL DFXMS5 ( NM )
*
*  VS FORTRAN --
*      CHARACTER*16 NM
*      CALL DFXMS5 ( NM )
*
*  PASCAL --
*    PROCEDURE DFXMS5 ( VAR JN : ALFA );  FORTRAN ; ....
         SPACE 4
DFXMS5   CSECT
         SPACE 2
*  SAVE CALLING PROGRAM'S REGISTERS
         STM   14,12,12(13)
         LR    12,15
         USING  DFXMS5,12
         SPACE 2
*  COPY 16 CHARACTERS FROM CVT-X'18' TO THE USERS FIRST
*  (AND PROBABLY ONLY) PARAMETER. ALWAYS 16 CHARACTERS PLANTED.
         L     2,16          CVT
         LA    3,X'18'
         SR    2,3           CVT-X'18'
*
         L     3,0(1)              ADDRESS OF PARAM 1
         MVC   0(16,3),0(2)        PASS TEXT BACK TO CALLING PROG
*
* RESTORE SAVED REGISTERS
         LM    2,12,28(13)
         L     14,12(13)
         MVI   12(13),X'FF'
         SR    15,15
         SPACE 2
* RETURN TO CALLING PROGRAM
         BR    14
         END
