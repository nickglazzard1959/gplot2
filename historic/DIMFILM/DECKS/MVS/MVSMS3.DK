*DECK MVSMS3 F
DFXMS3   TITLE 'GET THE JOBNAME FROM THE SYSTEM'
*
*              D F X M S 3
*
*  A FORTRAN CALLABLE ROUTINE TO RETURN THE 8 CHARACTER JOBNAME.
*
*                AANDI INSTON         30 APR 82
*
*  RETURNS AN EIGHT BYTE VALUE TO THE FIRST ARGUMENT.
*
*  EXAMPLES:
*
*  FORTRAN H --
*
*      LOGICAL*1 JN(8)
*      CALL DFXMS3 ( JN )
*
*  VS FORTRAN --
*      CHARACTER*8 JN
*      CALL DFXMS3 ( JN )
*
*  PASCAL --
*    PROCEDURE DFXMS3 ( VAR JN : ALFA );  FORTRAN ; ....
         SPACE 4
DFXMS3   CSECT
         SPACE 2
*  SAVE CALLING PROGRAM'S REGISTERS
         STM   14,12,12(13)
         SPACE 2
*  COPY THE JOBNAME (8 CHARACTERS) FROM THE TIOT TO THE USERS FIRST
*  (AND PROBABLY ONLY) PARAMETER. ALWAYS 8 CHARACTERS PLANTED.
         L     2,16          CVT
         L     2,0(2)        TCBWRDS
         L     2,4(2)        TCB
         L     2,12(2)       TIOT - ADDRESS OF JOBNAME
         SPACE 1
         L     3,0(1)        ADDRESS OF PARAM. 1
         MVC   0(8,3),0(2)   COPY JOBNAME
         SPACE 2
* RESTORE SAVED REGISTERS
         LM    2,12,28(13)
         L     14,12(13)
         MVI   12(13),X'FF'
         SR    15,15
         SPACE 2
* RETURN TO CALLING PROGRAM
         BR    14
         END
