*NOSEQ
.PROC,BUILD*I,ARG=(*N=NONE,*F),OPT=(*N=0,*S1..1/D),PARM=(*N=NONE,*F).
.IF,$ARG$.EQ.$NONE$.OR.$ARG$.EQ.$GPLOT$,DOGPLOT.
SETTL,*.
SETJSL,*.
.* BUILD THE GPLOT PROGRAM.
.* ASSUMES LIBRARIES HAVE BEEN BUILT.
NOTE./BUILDING THE GPLOT PROGRAM.
NOTE./ASSUMES GPLOT LIBRARIES HAVE BEEN BUILT.
ATTACH,LBUTILS.
ATTACH,LBDIMFM.
ATTACH,LBGRDEV.
RETURN,COMPILE.
MODCOMP,FTGPLOT,PLGPLOT.
RETURN,BNGPLOT.
RETURN,GPLIST.
RETURN,GPMAP.
FTN5,REW,I=COMPILE,B=BNGPLOT,#OPT=OPT,L=GPLIST.
RETURN,ABGPLOT.
LDSET,LIB.
LDSET,LIB=LBDIMFM/LBGRDEV/LBUTILS/SYMLIB.
LDSET,USEP=DFXB00.
LDSET,USEP=EPSBDB.
LDSET,USEP=SVGBLKD.
LDSET,PRESET=ZERO.
LDSET,MAP=X/GPMAP.
LOAD,BNGPLOT.
NOGO,ABGPLOT.
RETURN,BNGPLOT.
PURGE,GPLOT/NA.
DEFINE,TEMP=GPLOT.
REWIND,ABGPLOT.
COPYEI,ABGPLOT,TEMP.
RETURN,ABGPLOT.
RETURN,TEMP.
NOTE./OBTAINING FONTS.
RETURN,COMPILE.
MODCOMP,DADIMFO,PLGPLOT.
PURGE,DADIMFO/NA.
DEFINE,TEMP=DADIMFO.
REWIND,COMPILE.
COPYEI,COMPILE,TEMP.
RETURN,COMPILE.
RETURN,TEMP.
.ENDIF,DOGPLOT.
.*
.IF,$ARG$.EQ.$DIMFILM$,DODMF.
SETTL,*.
SETJSL,*.
.IF,$PARM$.EQ.$NONE$,DODFULL.
NOTE./BUILDING THE FULL DIMFILM LIBRARY.
RETURN,COMPILE.
RETURN,PLDIMFM.
GET,PLDIMFM.
NOTE./PREPROCESSING SOURCE.
MODIFY,P=PLDIMFM,C,L=0,I=0,F.
NOTE./COMPILING.
FTN5,REW,I=COMPILE,L=0,#OPT=OPT.
NOTE./GENERATING LIBRARY.
RETURN,LBTEMP.
LIBGEN,P=LBTEMP,N=LBDIMFM.
PURGE,LBDIMFM/NA.
RETURN,LBDIMFM.
DEFINE,LBDIMFM.
REWIND,LBTEMP.
COPYBF,LBTEMP,LBDIMFM.
RETURN,LBTEMP,COMPILE.
NOTE./DIMFILM BUILD COMPLETE.
.ELSE,DODFULL.
NOTE./BUILD AND REPLACE SINGLE MODULE IN DIMFILM LIBRARY.
RETURN,COMPILE,NEW,PLDIMFM,LBDIMFM,LGO.
GET,PLDIMFM.
ATTACH,LBDIMFM.
MODCOMP,PARM,PLDIMFM.
NOTE./#PARM=PARM
FTN5,REW,I=COMPILE,L=0,#OPT=OPT.
REWIND,LGO.
LIBEDIT,U,P=LBDIMFM,Z.\*REPLACE REL/PARM
RETURN,LBDIMFM.
PURGE,LBDIMFM.
DEFINE,LBDIMFM.
REWIND,NEW.
COPYBF,NEW,LBDIMFM.
RETURN,COMPILE,NEW,PLDIMFM,LBDIMFM,LGO.
NOTE./DIMFILM MODULE UPDATE COMPLETE.
.ENDIF,DODFULL.
.ENDIF,DODMF.
.*
.IF,$ARG$.EQ.$DEVICES$,DODEVS.
SETTL,*.
SETJSL,*.
NOTE./BUILDING GRAPHICS DEVICES LIBRARY.
RETURN,COMPILE.
RETURN,PLGRDEV.
GET,PLGRDEV.
NOTE./PREPROCESSING SOURCE.
MODIFY,P=PLGRDEV,C,L=0,I=0,F.
NOTE./COMPILING.
FTN5,REW,I=COMPILE,L=0,#OPT=OPT.
NOTE./GENERATING LIBRARY.
RETURN,LBTEMP.
LIBGEN,P=LBTEMP,N=LBGRDEV.
PURGE,LBGRDEV/NA.
RETURN,LBGRDEV.
DEFINE,LBGRDEV.
REWIND,LBTEMP.
COPYBF,LBTEMP,LBGRDEV.
RETURN,LBTEMP,COMPILE.
NOTE./LBGRDEV BUILD COMPLETE.
.ENDIF,DODEVS.
REVERT,NOLIST.
EXIT.
REVERT,ABORT.GPLOT BUILD FAILED.
.WEOR
